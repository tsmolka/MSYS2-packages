# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgbase=libtasn1
pkgname=('libtasn1' 'libtasn1-devel')
pkgver=4.9
pkgrel=1
pkgdesc="A library for Abstract Syntax Notation One (ASN.1) and Distinguish Encoding Rules (DER) manipulation."
arch=('i686' 'x86_64')
license=('GPL3' 'LGPL')
url="https://www.gnu.org/software/libtasn1/"
makedepends=('gcc' 'texinfo' 'make' 'patch')
source=(https://ftp.gnu.org/gnu/libtasn1/${pkgname}-${pkgver}.tar.gz{,.sig}
        libtasn1-3.3-msys2.patch)
sha256sums=('4f6f7a8fd691ac2b8307c8ca365bad711db607d4ad5966f6938a9d2ecd65c920'
            'SKIP'
            'd6934f9cc97d01df148cf879961a96cb41e8874d31b4f89d0b80247aabbc4143')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i $srcdir/libtasn1-3.3-msys2.patch
  autoreconf -i
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --build=${CHOST} --prefix=/usr
  make
  make DESTDIR=$srcdir/dest install
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package_libtasn1() {
  depends=('info')
  groups=('libraries')
  install=libtasn1.install

  mkdir -p $pkgdir/usr/bin
  cp -f $srcdir/dest/usr/bin/*.dll $pkgdir/usr/bin/
  cp -rf $srcdir/dest/usr/share $pkgdir/usr/
}

package_libtasn1-devel() {
  pkgdesc="Libtasn1 headers and libraries"
  groups=('development')
  depends=("libtasn1=${pkgver}")

  mkdir -p $pkgdir/usr/bin
  cp -f $srcdir/dest/usr/bin/*.exe $pkgdir/usr/bin/
  cp -rf $srcdir/dest/usr/include $pkgdir/usr/
  cp -rf $srcdir/dest/usr/lib $pkgdir/usr/
}
