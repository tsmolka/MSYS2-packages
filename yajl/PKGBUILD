# Contributor: Tobias Smolka <tsmolka@gmail.com>
pkgname="yajl"
pkgver=2.1.0
pkgrel=1
pkgdesc='Yet Another JSON Library.'
arch=('any')
url='http://lloyd.github.com/yajl/'
license=('ISC')
source=(
  "$pkgname-$pkgver.tar.gz::https://github.com/lloyd/$pkgname/archive/$pkgver.tar.gz"
  '01-fix-compiler.patch'
  '02-fix-tests.patch'
)
sha256sums=(
  '3fb73364a5a30efe615046d07e6db9d09fd2b41c763c5f7d3bfb121cd5c5ac5a'
  'fa629191df14f32d54cc03999e9913ed2afa83c032249f9399ee93d87380b49a'
  '2345c74a3a005f4bb5a9ff49bc0700d41672b742eea0775a3637f164118f9195'
)
makedepends=(
  "make"
  "patch"
  "gcc"
  "cmake"
  "diffutils"
)
prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  patch -p1 -i "${srcdir}/01-fix-compiler.patch"
}
build() {
  mkdir -p "${srcdir}/build-${CARCH}"
  cd "${srcdir}/build-${CARCH}"
  
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  cmake \
    -G"Unix Makefiles" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX= \
    ../${pkgname}-${pkgver}
  
  make
}
check() {
  cd "${srcdir}/build-${CARCH}"

  make test
  PATH="$PATH:${srcdir}/build-${CARCH}/${pkgname}-${pkgver}/lib" make test-api
}
package() {
  cd "${srcdir}/build-${CARCH}"
  
  make DESTDIR=${pkgdir} install  
  mv ${pkgdir}/lib/*.dll ${pkgdir}/bin/
  
  install -Dm644 "../${pkgname}-${pkgver}/COPYING" \
    "${pkgdir}/share/licenses/${pkgname}/LICENSE"
}
