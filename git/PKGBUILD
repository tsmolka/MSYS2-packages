# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Maintainer: Ray Donnelly <mingw.android@gmail.com>

pkgname=git
pkgver=2.10.1
pkgrel=1
pkgdesc="The fast distributed version control system"
arch=('i686' 'x86_64')
url="https://git-scm.com/"
license=('GPL2')
groups=('VCS')
depends=('curl'
         'expat>=2.0'
         #'info'
         'libpcre'
         'vim'
         'openssh'
         'openssl'
         'perl-Error'
         'perl>=5.14.0'
         'perl-Authen-SASL'
         'perl-libwww'
         'perl-MIME-tools'
         'perl-Net-SMTP-SSL'
         'perl-TermReadKey')
makedepends=('asciidoc'
             'docbook-xml'
             'docbook-xsl'
             #'docbook2x'
             'less'
             'libcurl-devel'
             'libiconv-devel'
             'pcre-devel'
             #'python2'
             'rsync'
             #'texinfo'
             'xmlto')
optdepends=(#'tk: gitk and git gui'
            'python2: various helper scripts'
            'subversion: git svn'
            #'cvsps: git cvsimport'
            )
#install=git.install
replaces=('git-core')
provides=('git-core')
#options=('debug' '!strip')
source=("${pkgname}-${pkgver}.tar.gz"::https://github.com/git/git/archive/v${pkgver}.tar.gz
        1.7.9-cygwin.patch
        git-1.9.0-manifest-msys2.patch
        git-1.8.4-msys2.patch
        git-2.3.5-mingw-pwd.patch
        git-2.8.2-Cygwin-Allow-DOS-paths.patch)
sha256sums=('b37b8f364f4b1cd8b82feee53d7ebf129e588901d21e3feaabaae9e51facb3ad'
            'c5e735d829e11f79e2d508b663d0924030498f48fc716881031fb975dbf187a5'
            '3c0ed027540a537617590dc2c09d449bb137badd5ee44893e65e450d95dc765d'
            'a82df3fc9a80b9f9003a9b9feca063413b14630c043c0b2b88bf2c61c46154cc'
            'f67412f829e026b677d4c3ceacfb21da0549ad4636fb07c283e52c3fd5aa3e22'
            'fb0e003f686a6d7c676d40144d16d4ba4ff38f28f7928e4401d95f198029f35e')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  rm -f compat/win32/git.manifest compat/win32/resource.rc
  patch -p2 -i "${srcdir}"/1.7.9-cygwin.patch
  patch -p1 -i "${srcdir}"/git-1.8.4-msys2.patch
  patch -p1 -i "${srcdir}"/git-1.9.0-manifest-msys2.patch
  patch -p2 -i "${srcdir}"/git-2.3.5-mingw-pwd.patch
  patch -p1 -i "${srcdir}"/git-2.8.2-Cygwin-Allow-DOS-paths.patch

  local _arch=
  if [ "${CARCH}" == 'x86_64' ]; then
    _arch='amd64'
  elif [ "${CARCH}" == 'i686' ]; then
    _arch='x86'
  fi
  sed -e "s|@ARCH@|${_arch}|g" -i compat/win32/git.manifest

  make configure
}

build() {
  export PYTHON_PATH='/usr/bin/python2'
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --build=${CHOST} \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --with-libpcre \
    --with-editor=vim \
    --htmldir=/usr/share/doc/git/html \
    --mandir=/usr/share/man

  make INSTALLDIRS=vendor all
  make -j1 man
  #make -j1 info
  make -C contrib/subtree prefix=/usr all
}

check() {
  export PYTHON_PATH='/usr/bin/python2'
  cd "${srcdir}/${pkgname}-${pkgver}"
  local jobs
  jobs=$(expr "$MAKEFLAGS" : '.*\(-j[0-9]*\).*')
  mkdir -p /tmp/git-test
  # We used to use this, but silly git regressions:
  #GIT_TEST_OPTS="--root=/dev/shm/" \
  # http://comments.gmane.org/gmane.comp.version-control.git/202020
  make \
    NO_SVN_TESTS=y \
    DEFAULT_TEST_TARGET=prove \
    GIT_PROVE_OPTS="$jobs -Q" \
    GIT_TEST_OPTS="--root=/tmp/git-test" \
    test
}

package() {
  export PYTHON_PATH='/usr/bin/python2'
  cd "${srcdir}/${pkgname}-${pkgver}"
  make INSTALLDIRS=vendor DESTDIR="$pkgdir" install
  make INSTALLDIRS=vendor DESTDIR="${pkgdir}" install-man
  #make INSTALLDIRS=vendor DESTDIR="${pkgdir}" install-info

  # bash completion
  mkdir -p "${pkgdir}"/usr/share/bash-completion/completions/
  install -m644 ./contrib/completion/git-completion.bash "${pkgdir}"/usr/share/bash-completion/completions/git
  # fancy git prompt
  mkdir -p "${pkgdir}"/usr/share/git/
  install -m644 ./contrib/completion/git-prompt.sh "${pkgdir}"/usr/share/git/git-prompt.sh
  # subtree installation
  sed "s|libexec/git-core|lib/git-core|" -i contrib/subtree/Makefile
  make -C contrib/subtree prefix=/usr DESTDIR="${pkgdir}" install

  # the rest of the contrib stuff
  cp -a ./contrib/* ${pkgdir}/usr/share/git/

  # scripts are for python 2.x
  sed -i 's|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|' \
    $(find "$pkgdir" -name '*.py') \
    "${pkgdir}"/usr/lib/git-core/git-p4 \
    "${pkgdir}"/usr/share/git/gitview/gitview \
    "${pkgdir}"/usr/share/git/remote-helpers/git-remote-bzr \
    "${pkgdir}"/usr/share/git/remote-helpers/git-remote-hg
  sed -i 's|#![ ]*/usr/bin/python$|#!/usr/bin/python2|' \
    "${pkgdir}"/usr/share/git/svn-fe/svnrdump_sim.py

  # remove perllocal.pod, .packlist, and empty directories.
  find ${pkgdir}/usr/lib/perl5 -name *.pod -delete
  find ${pkgdir}/usr/lib/perl5 -name .packlist -delete
}
