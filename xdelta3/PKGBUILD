# Maintainer: David Macek <david.macek.0@gmail.com>

pkgname=xdelta3
pkgver=3.0.11
pkgrel=1
pkgdesc='Diff utility which works with binary files'
arch=('x86_64' 'i686')
url='http://xdelta.org/'
license=('Apache 2.0')
depends=('xz' 'liblzma')
makedepends=('liblzma-devel')
source=("https://github.com/jmacd/${pkgname/3}/archive/v$pkgver.tar.gz")
sha256sums=('28278a4d73127f3d2b00bbde179f8ee1f289ccd3f7f2ac7cd837f6580f90a7b7')
noextract=("v$pkgver.tar.gz")

prepare() {
  # Note: current release contains symlink (xdelta3/py-compile) which can't be extracted
  bsdtar -x -f "${srcdir}/v$pkgver.tar.gz" || true
  
  cd "${pkgname/3}-${pkgver}/${pkgname}"
  libtoolize --force
  aclocal
  autoheader
  automake --force-missing --add-missing
  autoconf
}

check() {
  cd "${pkgname/3}-${pkgver}/${pkgname}"
  ./$pkgname test
}

build() {
  cd "${pkgname/3}-${pkgver}/${pkgname}"
  ./configure --prefix=/usr --build=${CHOST}
  make
}

package() {
  cd "${pkgname/3}-${pkgver}/${pkgname}"
  make DESTDIR="${pkgdir}" install
}
