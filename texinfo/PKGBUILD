# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=('texinfo' 'info' 'texinfo-tex')
pkgver=6.1
pkgrel=1
pkgdesc="Utilities to work with and produce manuals, ASCII text, and on-line documentation from a single source file"
arch=('i686' 'x86_64')
url="https://www.gnu.org/software/texinfo/"
license=('GPL3')
makedepends=('ncurses-devel' 'gettext-devel' 'findutils' 'gzip' 'perl')
source=(https://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz{,.sig}
        6.0.93-1-fix_interactive_tests.patch
        invalid.patch
        help.patch
        0001-texinfo-5.1-msys2.patch
        0002-manifest.all.patch
        0003-be-content-with-older-gettext.msys2.patch
        0004-no-w32-stuff.msys2.patch
        0005-fix-makeinfo-exeext.msys2.patch)
sha256sums=('ac68394ce21b2420ba7ed7cec65d84aacf308cc88e9bf4716fcfff88286883d2'
            'SKIP'
            'ba4a35cee96731d2dbbce3b13fb04fe443ea35b86479bc0227271a9f5547d19f'
            'bffe0615027b29901ea4be42fd00a7f3dd20f6fa820a61de6eb1aa88a427bfc7'
            '61f0ea5064bc871c4fdb7809a75ec4e41ae37f76d18bd2ac973a10d74ba9b3ec'
            '78d58e51288fae64b73084d9f275621628a7100d5d79192329c506785a06eb61'
            '5bc6680fa441b26ed18a794ad201a1252dba9f03d5426c87e42d33c758a4a039'
            '8631da81d0ee9531afb65df3deeb56a9893b0ee0c13e396449905451d094878b'
            'ccf696f2777ebd2c85c4cb311a4735957ffa40952ecbe50c7031b9f05dc6cdee'
            'e5b18f11d9a5d99f96f9228266e6204cfb2e076e2d9c336db0eceb72e2252873')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p2 -i ${srcdir}/6.0.93-1-fix_interactive_tests.patch
  patch -p0 -i ${srcdir}/invalid.patch
  patch -p2 -i ${srcdir}/help.patch
  
  patch -p1 -i ${srcdir}/0001-texinfo-5.1-msys2.patch
  patch -p1 -i ${srcdir}/0002-manifest.all.patch
  #patch -p1 -i ${srcdir}/0003-be-content-with-older-gettext.msys2.patch
  patch -p1 -i ${srcdir}/0004-no-w32-stuff.msys2.patch
  patch -p1 -i ${srcdir}/0005-fix-makeinfo-exeext.msys2.patch

  autoreconf -fi
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --build=${CHOST} \
    --prefix=/usr \
    --without-libiconv-prefix \
    --without-libintl-prefix \
    gl_cv_func_wcwidth_works=yes

  make
  make DESTDIR=${srcdir}/dest install
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package_texinfo() {
  depends=('info' 'perl' 'sh')
  groups=('base-devel')
  install=texinfo.install

  mkdir -p ${pkgdir}/usr/bin
  mkdir -p ${pkgdir}/usr/share/info
  mkdir -p ${pkgdir}/usr/share/man/man{1,5}
  for ff in makeinfo pod2texi texi2any; do
    cp -f ${srcdir}/dest/usr/bin/${ff} ${pkgdir}/usr/bin/${ff}
    cp -f ${srcdir}/dest/usr/share/man/man1/${ff}.1 ${pkgdir}/usr/share/man/man1/${ff}.1
  done
  cp -rf ${srcdir}/dest/usr/share/locale ${pkgdir}/usr/share/
  cp -rf ${srcdir}/dest/usr/share/texinfo ${pkgdir}/usr/share/
  cp -f ${srcdir}/dest/usr/share/man/man5/texinfo.5 ${pkgdir}/usr/share/man/man5/
  cp -f ${srcdir}/dest/usr/share/info/texinfo.info{,-1,-2,-3} ${pkgdir}/usr/share/info/
}

package_info() {
  depends=('ncurses' 'libintl' 'gzip')
  groups=(base)
  install=info.install

  mkdir -p ${pkgdir}/usr/{bin,share}
  mkdir -p ${pkgdir}/usr/share/info
  mkdir -p ${pkgdir}/usr/share/man/man{1,5}
  for ff in info install-info; do
    cp -f ${srcdir}/dest/usr/bin/${ff}.exe ${pkgdir}/usr/bin/${ff}.exe
    cp -f ${srcdir}/dest/usr/share/man/man1/${ff}.1 ${pkgdir}/usr/share/man/man1/${ff}.1
  done
  cp -f ${srcdir}/dest/usr/share/man/man5/info.5 ${pkgdir}/usr/share/man/man5/info.5
  cp -f ${srcdir}/dest/usr/share/info/info-stnd.info ${pkgdir}/usr/share/info/info-stnd.info
}

package_texinfo-tex() {
  depends=('perl' 'sh')
  groups=('base-devel')

  mkdir -p ${pkgdir}/usr/{bin,share}
  mkdir -p ${pkgdir}/usr/share/man/man1
  for ff in texindex texi2dvi texi2pdf pdftexi2dvi; do
    cp -f ${srcdir}/dest/usr/bin/${ff} ${pkgdir}/usr/bin/${ff}
    cp -f ${srcdir}/dest/usr/share/man/man1/${ff%.*}.1 ${pkgdir}/usr/share/man/man1/${ff%.*}.1
  done
}
