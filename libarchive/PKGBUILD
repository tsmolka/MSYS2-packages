# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=('libarchive' 'libarchive-devel' 'bsdcpio' 'bsdtar')
pkgver=3.2.1
pkgrel=1
pkgdesc="library that can create and read several streaming archive formats"
arch=('i686' 'x86_64')
url="http://libarchive.org/"
license=('BSD')
groups=('libraries')
depends=('gcc-libs' 'libbz2' 'libiconv' 'liblzma' 'liblzo2' 'libnettle' 'libxml2' 'zlib')
makedepends=('libbz2-devel' 'libiconv-devel' 'liblzma-devel' 'liblzo2-devel' 'libnettle-devel' 'libxml2-devel' 'zlib-devel')
options=('!strip' 'debug' 'libtool')
# provides=('libarchive.so')
source=("http://libarchive.org/downloads/$pkgname-$pkgver.tar.gz"
        'libarchive-3.2.0-msys2.patch')
sha256sums=('72ee1a4e3fd534525f13a0ba1aa7b05b203d186e0c6072a8a4738649d0b3cfd2'
            '639f316243509470c6f8132e59104a49bfb8df61be9722887d145a1b7262286c')

prepare() {
  cd "$pkgname-$pkgver"

  # msysize patch
  patch -Np1 -i "$srcdir/libarchive-3.2.0-msys2.patch"

  autoreconf -ivf
}

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --enable-shared \
    --enable-static \
    --without-libiconv-prefix

  make
  make DESTDIR="${srcdir}/dest" install
}

check() {
  cd "$pkgname-$pkgver"

  #make check | true
}

package_bsdcpio() {
  groups=('base')

  mkdir -p $pkgdir/usr/bin
  mkdir -p $pkgdir/usr/share/man/man1/
  cp -f ${srcdir}/dest/usr/bin/bsdcpio.exe $pkgdir/usr/bin/
  cp -f ${srcdir}/dest/usr/share/man/man1/bsdcpio.* $pkgdir/usr/share/man/man1/
}

package_bsdtar() {
  groups=('base')

  mkdir -p $pkgdir/usr/bin
  mkdir -p $pkgdir/usr/share/man/man1/
  cp -f ${srcdir}/dest/usr/bin/bsdtar.exe $pkgdir/usr/bin/
  cp -f ${srcdir}/dest/usr/share/man/man1/bsdtar.* $pkgdir/usr/share/man/man1/
}

package_libarchive() {
  mkdir -p $pkgdir/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*.dll $pkgdir/usr/bin/

  install -D -m644 ${srcdir}/${pkgname}-${pkgver}/COPYING $pkgdir/usr/share/licenses/libarchive/COPYING
}

package_libarchive-devel() {
  groups=('development')
  options=('staticlibs')
  depends=("libarchive=${pkgver}" 'libbz2-devel' 'libiconv-devel' 'liblzma-devel' 'liblzo2-devel' 'libnettle-devel' 'libxml2-devel' 'zlib-devel')

  mkdir -p $pkgdir/usr
  cp -rf ${srcdir}/dest/usr/include $pkgdir/usr/
  cp -rf ${srcdir}/dest/usr/lib $pkgdir/usr/
}
