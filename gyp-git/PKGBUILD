# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Maintainer: Ray Donnelly <ray.donnelly@gmail.com>

_realname=gyp
pkgname=${_realname}-git
pkgver=r2097.eeb0738
pkgrel=1
pkgdesc="GYP can Generate Your Projects."
url="https://code.google.com/p/gyp/"
arch=('i686' 'x86_64')
provides=("gyp")
conflicts=("gyp")
replaces=("gyp-svn")
makedepends=('git')
depends=('python2' 'python2-setuptools')
license=('custom')
source=("git+https://chromium.googlesource.com/external/gyp"
        '0001-msys-ize.patch'
        'issue2019133002_1.diff')
sha256sums=('SKIP'
            '5e402b570bfeb8c95fec6ddd55afbe316d9bb4b04090c8f09cff6664bd00297e'
            '39732ad06e88f2195acc348231dc93eb20aa5fbffbe05ffef5c29344f9a264ec')

pkgver() {
  cd "${srcdir}"/${_realname}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}"/${_realname}
  git am "${srcdir}"/0001-msys-ize.patch
  
  #https://codereview.chromium.org/2019133002/
  patch -p1 -i "${srcdir}"/issue2019133002_1.diff
}

package() {
  cd "${srcdir}"/${_realname}
  python2 setup.py install --root="${pkgdir}"
}
