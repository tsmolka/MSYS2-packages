# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=file
pkgver=5.28
pkgrel=2
pkgdesc="File type identification utility"
arch=('i686' 'x86_64')
license=('custom')
groups=('base' 'base-devel')
url="http://www.darwinsys.com/file/"
depends=('gcc-libs' 'msys2-runtime' 'zlib')
makedepends=('python2' 'python3' 'zlib-devel')
options=('!libtool')
source=(#ftp://ftp.astron.com/pub/${pkgname}/${pkgname}-${pkgver}.tar.gz
        https://fossies.org/linux/misc/${pkgname}-${pkgver}.tar.gz
        file-5.17-msys2.patch
        fix-missing-parenthesis.patch)
sha256sums=('0ecb5e146b8655d1fa84159a847ee619fc102575205a0ff9c6cc60fc5ee2e012'
            'fa8a8581df5535981cbfb9e9f3e64edf34085f959cec42f88e0a13b8aa0e919b'
            '942efc6f061608ebc4e66337476bed9568800e809158c7d9836d125dd27d1b7c')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ${srcdir}/file-5.17-msys2.patch
  patch -p1 -i ${srcdir}/fix-missing-parenthesis.patch

  autoreconf -fi
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure \
    --prefix=/usr \
    --build=${CHOST} \
    --host=${CHOST} \
    --target=${CHOST} \
    --enable-fsect-man5

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR=${pkgdir} install

  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING

  PYTHON_SITELIB=$(python2 -c 'from distutils.sysconfig import * ; print get_python_lib(0,0);')
  PYTHON3_SITELIB=$(python3 -c 'from distutils.sysconfig import * ; print(get_python_lib(0,0));')

  mkdir -p ${pkgdir}/$PYTHON_SITELIB
  mkdir -p ${pkgdir}/$PYTHON3_SITELIB
  cp -f ${srcdir}/${pkgname}-${pkgver}/python/magic.py ${pkgdir}/$PYTHON_SITELIB
  cp -f ${srcdir}/${pkgname}-${pkgver}/python/magic.py ${pkgdir}/$PYTHON3_SITELIB

  cat ${srcdir}/${pkgname}-${pkgver}/magic/Magdir/* > ${pkgdir}/usr/share/misc/magic
  cp -rf ${pkgdir}/usr/share/misc/magic ${pkgdir}/usr/share/magic
  mkdir -p ${pkgdir}/usr/share/file
  cp -rf ${pkgdir}/usr/share/magic ${pkgdir}/usr/share/file/magic
}
