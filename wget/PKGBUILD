# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=wget
pkgver=1.18
pkgrel=1
pkgdesc="A network utility to retrieve files from the Web"
arch=('i686' 'x86_64')
url="https://www.gnu.org/software/wget/"
license=('GPL3')
groups=('base-devel')
depends=('gcc-libs' 'libiconv' 'libidn' 'libintl' 'libgpgme' 'libmetalink' 'libpcre' 'libuuid' 'openssl' 'zlib')
makedepends=('gettext-devel' 'libidn-devel' 'libgpgme-devel' 'libmetalink-devel' 'libuuid-devel' 'openssl-devel' 'pcre-devel' 'zlib-devel' 'python3')
checkdepends=('perl-HTTP-Daemon' 'perl-IO-Socket-SSL')
optdepends=('ca-certificates: HTTPS downloads')
backup=('etc/wgetrc')
install=wget.install
source=(https://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz{,.sig}
        1.13.4-sysconfdir.patch
        wget-1.14-msys2.patch
        fix-gettext-version.patch
        fix-sys_select.patch)
sha256sums=('b5b55b75726c04c06fe253daec9329a6f1a3c0c1878e3ea76ebfebc139ea9cc1'
            'SKIP'
            '7096f08a97befd225cf2bc6dfbdd2631a260bbb9496644c98ad198548969a40b'
            '760fb3edb326a5fc303de3bed66214de132cc4ab1acdb76623b3aabbac26a240'
            'abedb6bae932fb3cda5d9c509fd0e7cd7fcf6f89c31feabc979ee624d5b130ef'
            '7c7dfd70101e4b8140e46b8c9a5c3f613f5454d28864a3922d9b3a491d1db73a')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p2 -i "${srcdir}/1.13.4-sysconfdir.patch"
  patch -p1 -i "${srcdir}/wget-1.14-msys2.patch"
  patch -p1 -i "${srcdir}/fix-gettext-version.patch"
  patch -p1 -i "${srcdir}/fix-sys_select.patch"

  autoreconf -fiv
  cat >> doc/sample.wgetrc <<EOF

# default root certs location
ca_certificate=/usr/ssl/certs/ca-bundle.crt
EOF
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --build=${CHOST} \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --with-ssl=openssl \
    --without-libiconv-prefix \
    --without-libintl-prefix \
    --without-libpth-prefix \
    --without-libssl-prefix \
    --without-libgnutls-prefix

  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
